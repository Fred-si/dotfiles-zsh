#!/bin/zsh
# TODO Changer la couleur de l'heure en fonction du moment de la journée
# TODO Différencier le prompt pour root
# TODO Aéré le code et nettoyer le code

prompt_forkclint_help () { # [[[
	cat <<'EOF'
	Modification du prompt clint par Frédéric Sierra
EOF
} # ]]]

prompt_forkclint_setup () { # [[[
	local -a p_color
	local -A p_caractere
	local p_date p_tty p_platform p_version p_userpwd p_apm p_shlvlhist p_rc p_end p_win

	autoload -Uz vcs_info

# Configuration des couleurs [[[
	if autoload colors && colors 2>/dev/null ; then
	    BLUE="%{${fg[blue]}%}"
	    RED="%{${fg_bold[red]}%}"
	    GREEN="%{${fg[green]}%}"
	    CYAN="%{${fg[cyan]}%}"
	    MAGENTA="%{${fg[magenta]}%}"
	    YELLOW="%{${fg[yellow]}%}"
	    WHITE="%{${fg[white]}%}"
	    NO_COLOR="%{${reset_color}%}"
		REVERSE=$'%{\033[7m%}'
	else
	    BLUE=$'%{\e[1;34m%}'
	    RED=$'%{\e[1;31m%}'
	    GREEN=$'%{\e[1;32m%}'
	    CYAN=$'%{\e[1;36m%}'
	    WHITE=$'%{\e[1;37m%}'
	    MAGENTA=$'%{\e[1;35m%}'
	    YELLOW=$'%{\e[1;33m%}'
	    NO_COLOR=$'%{\e[0m%}'
	fi

	p_color[1]=${1:-${${SSH_CLIENT+${RED}}:-${YELLOW}}}	# Si connecté via ssh la couleur sera rouge sinon jaune
	p_color[2]=${2:-${CYAN}}
	p_color[3]=${3:-${GREEN}}
	p_color[4]=${4:-${YELLOW}}
	p_color[5]=${5:-${WHITE}}
	p_color[6]=${6:-${RED}}
	p_color[7]=${6:-${MAGENTA}}

	p_caractere['\[']="$p_color[1]["
	p_caractere['\]']="$p_color[1]]"
	p_caractere['<']="$p_color[1]<"
	p_caractere['>']="$p_color[1]>"
	p_caractere['\(']="$p_color[1]("
	p_caractere['\)']="$p_color[1])"
# ]]]

# Création des diverses sections [[[
	p_date="$p_caractere['\[']$p_color[2]%D{%d/%m %T}$p_caractere['\]']"
	p_tty="$p_caractere['\[']$p_color[3]%l$p_caractere['\]']"
	p_platform="$p_caractere['\[']$p_color[2]${$(uname -sr):s/-ARCH/}$p_caractere['\]']"
	p_version="$p_caractere['\[']$p_color[2]ZSH ${ZSH_VERSION}$p_caractere['\]']"

	if [[ $BATTERY -gt 0 ]];then
		p_battery="$p_caractere['\(']$p_color[3]"
		p_battery+='${BATTERY_LEVEL}'
		p_battery+="$p_caractere['\)']"
	fi

	p_userpwd="$p_caractere['<']$p_color[3]%n$p_color[5]@$p_color[7]%m$p_battery$p_color[5]:$p_color[4]%~$p_caractere['>']"
	p_vcs='${vcs_info_msg_0_}'

	p_shlvlhist="%f%(2L./$SHLVL.) %h "
	p_rc="%(?..[%?%1v] )"
	p_end="%f%# "
# ]]]

	zle_highlight[(r)default:*]=default:$p_color[2]

# Assemblage des prompts [[[
prompt="%(#.$REVERSE.)$p_date$p_platform
$p_userpwd
$p_shlvlhist$p_rc$p_vcs$p_end$NO_COLOR"

	PS2='%(4_.\.)%3_> %E'
# ]]]

	add-zsh-hook precmd prompt_forkclint_precmd

} # ]]]

prompt_forkclint_precmd () { # [[[
	setopt noxtrace noksharrays localoptions
	local exitstatus=$?
	local git_dir git_ref

	psvar=()
	[[ $exitstatus -ge 128 ]] && psvar[1]=" $signals[$exitstatus-127]" ||
	psvar[1]=""

	[[ -o interactive ]] && jobs -l

	vcs_info
	[[ -n $vcs_info_msg_0_ ]] && psvar[2]="$vcs_info_msg_0_"

	if [[ $BATTERY -gt 0 ]];then
		battery
	fi

	# Ring a bell
	echo -ne '\007'
} # ]]]

prompt_forkclint_setup "$@"
# vim: foldmethod=marker: foldmarker=[[[,]]]:
