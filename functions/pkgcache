#!/usr/bin/env zsh

pkgcache() {
    usage() {
        echo "Usage: ${0} (-c|--clean)"
        echo "Clear package cache"
    }

    if [[ $1 != '-c' ]] && [[ $1 != '--clean' ]]; then
        usage
        return 1
    fi

	if [[ $USER != 'root' ]]; then
		su -c 'source $HOME/.zshenv && source $ZDOTDIR/zshrc && pkgcache -c' -l
		return 0
	fi

    cache_directory=/var/cache/pacman/pkg

    if where yay 2>&1 >/dev/null; then
        pkg_manager=yay
    else
        pkg_manager=pacman
    fi

    echo "Clear installed packages cache"
    paccache -rk3

    echo "Clear outdated uninstalled packages cache"
    for filename in $(paccache -duvk0 |grep '.pkg.' |grep -vP '\.sig$'); do
        cache_infos=(${(s:-:)filename})
        pkg_name=${(j:-:)${cache_infos[1,-4]}}

        pkg_infos=$($pkg_manager -Si $pkg_name 2>&1)

        cache_version=${(j:-:)cache_infos[-3,-2]}
        pkg_version=$(echo $pkg_infos |grep Version |grep -Po '\S+$')

        if [[ -z $pkg_infos ]] || [[ $cache_version != $pkg_version ]]; then
            rm -f ${cache_directory}/${filename}{,.sig}
        fi
    done 

    echo
    du -sh $cache_directory
}
